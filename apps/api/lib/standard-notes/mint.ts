import { type Package, defaultPackage } from "@/lib/types";

const rust = "";

const masm = `use miden::protocol::active_note
use miden::protocol::note
use miden::standards::faucets::network_fungible->network_faucet

# CONSTANTS
# =================================================================================================

const MINT_NOTE_NUM_STORAGE_ITEMS_PRIVATE=6
const MINT_NOTE_MIN_NUM_STORAGE_ITEMS_PUBLIC=12

use miden::protocol::note::NOTE_TYPE_PUBLIC
use miden::protocol::note::NOTE_TYPE_PRIVATE

# Memory addresses of MINT note storage (private mode)
const STORAGE_PTR = 0
const PRIVATE_RECIPIENT_PTR = STORAGE_PTR
const PRIVATE_TAG_PTR = STORAGE_PTR + 4
const PRIVATE_AMOUNT_PTR = STORAGE_PTR + 5

# Memory addresses of MINT note storage (public mode)
const PUBLIC_SCRIPT_ROOT_PTR = STORAGE_PTR
const PUBLIC_SERIAL_NUM_PTR = STORAGE_PTR + 4
const PUBLIC_TAG_PTR = STORAGE_PTR + 8
const PUBLIC_AMOUNT_PTR = STORAGE_PTR + 9
const PUBLIC_OUTPUT_NOTE_STORAGE_PTR = STORAGE_PTR + 12

# ERRORS
# =================================================================================================

const ERR_MINT_UNEXPECTED_NUMBER_OF_STORAGE_ITEMS="MINT script expects exactly 6 storage items for private or 12+ storage items for public output notes"

#! Network Faucet MINT script: mints assets by calling the network faucet's mint_and_send
#! function.
#! This note is intended to be executed against a network fungible faucet account.
#!
#! Requires that the account exposes:
#! - miden::standards::faucets::network_fungible::mint_and_send procedure.
#!
#! Inputs:  [ARGS, pad(12)]
#! Outputs: [pad(16)]
#!
#! Note storage supports two modes. Depending on the number of note storage items,
#! a private or public note is created on consumption of the MINT note:
#!
#! Private mode (6 storage items) - creates a private note:
#! - RECIPIENT: The recipient digest (4 elements)
#! - tag: Note tag for the output note
#! - amount: The amount to mint
#!
#! Public mode (12+ storage items) - creates a public note with variable-length storage:
#! - SCRIPT_ROOT: Script root of the output note (4 elements)
#! - SERIAL_NUM: Serial number of the output note (4 elements)
#! - tag: Note tag for the output note
#! - amount: The amount to mint
#! - padding
#! - padding
#! - [STORAGE]: Variable-length storage for the output note (Vec<Felt>)
#!              The number of output note storage items = num_mint_note_storage_items - 12
#!
#! The padding is necessary since note::build_recipient expects a word-aligned storage_ptr.
#!
#! Panics if:
#! - account does not expose mint_and_send procedure.
#! - the number of storage items is not exactly 6 for private or less than 12 for public output notes.
@note_script
pub proc main
    dropw
    # => [pad(16)]
    # Load note storage into memory starting at STORAGE_PTR
    push.STORAGE_PTR exec.active_note::get_storage
    # => [num_storage_items, storage_ptr, pad(16)]

    dup
    # => [num_storage_items, num_storage_items, storage_ptr, pad(16)]

    u32assert2.err=ERR_MINT_UNEXPECTED_NUMBER_OF_STORAGE_ITEMS
    u32gte.MINT_NOTE_MIN_NUM_STORAGE_ITEMS_PUBLIC
    # => [is_public_output_note, num_storage_items, storage_ptr, pad(16)]

    if.true
        # public output note creation
        # => [num_storage_items, storage_ptr, pad(16)]

        movdn.9 drop
        # => [EMPTY_WORD, EMPTY_WORD, num_storage_items, pad(8)]

        mem_loadw_le.PUBLIC_SCRIPT_ROOT_PTR
        # => [SCRIPT_ROOT, EMPTY_WORD, num_storage_items, pad(8)]

        swapw mem_loadw_le.PUBLIC_SERIAL_NUM_PTR
        # => [SERIAL_NUM, SCRIPT_ROOT, num_storage_items, pad(8)]

        # compute variable length note storage for the output note
        movup.8 sub.MINT_NOTE_MIN_NUM_STORAGE_ITEMS_PUBLIC
        # => [num_output_note_storage, SERIAL_NUM, SCRIPT_ROOT, pad(8)]

        push.PUBLIC_OUTPUT_NOTE_STORAGE_PTR
        # => [storage_ptr, num_output_note_storage, SERIAL_NUM, SCRIPT_ROOT, pad(8)]

        exec.note::build_recipient
        # => [RECIPIENT, pad(12)]

        # push note_type, and load tag and amount
        push.NOTE_TYPE_PUBLIC
        mem_load.PUBLIC_TAG_PTR mem_load.PUBLIC_AMOUNT_PTR
        # => [amount, tag, note_type, RECIPIENT, pad(12)]
    else
        # private output note creation

        eq.MINT_NOTE_NUM_STORAGE_ITEMS_PRIVATE assert.err=ERR_MINT_UNEXPECTED_NUMBER_OF_STORAGE_ITEMS drop
        # => [storage_ptr, pad(16)]

        drop
        # => [pad(16)]

        mem_loadw_le.PRIVATE_RECIPIENT_PTR
        # => [RECIPIENT, pad(12)]

        # push note_type, and load tag and amount
        push.NOTE_TYPE_PRIVATE
        mem_load.PRIVATE_TAG_PTR mem_load.PRIVATE_AMOUNT_PTR
        # => [amount, tag, note_type, RECIPIENT, pad(12)]
    end
    # => [amount, tag, note_type, RECIPIENT, pad(12)]

    # mint_and_send expects 9 pad elements, returns 15 and 12 are provided here.
    # so the total number of pads after calling is 12 + (15-9) = 18
    call.network_faucet::mint_and_send
    # => [note_idx, pad(18))]

    drop drop drop
    # => [pad(16)]
end
`;

const mint: Package = {
  ...defaultPackage(),
  id: "mint",
  name: "mint",
  type: "note-script",
  rust,
  masm,
  digest: "0xef1864750cbdec29e885d0a79173d791669089dfae44c5f6ddce855b3aee17c5",
};

export default mint;
